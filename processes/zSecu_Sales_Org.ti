#region Prolog
#****Begin: Generated Statements***
#****End: Generated Statements****

################################################################
#                 DESCRIPTION DU PROCESSUS                     #
#                                                              #
# Date :   11/04/2019                                          #
#                                                              #
# Fait par :   Anne BESNARD                                    #
#                                                              #
# Action : Sécurité sur les sales org					       #
#															   #
#                                                              #
################################################################

#====================================#
#     Déclaration des variables      #
#====================================#

#--------------------Cubes--------------------#
C_ElementSecurity_Sales_Org = '}ElementSecurity_1_G_Sales_Org';

#--------------------Dimensions--------------------#
D_Sales_Org = '1_G_SALES_ORG';
D_Groups = '}Groups';

#--------------------Elements--------------------#



#--------------------Variables globales--------------------#
vDATE = today(1) | ' ' | TIME;
vLocalTime =  '_' | today(1) | '_' | subst(TIME,1,2) | 'h' | subst(TIME,4,2);
vTimeStart_Sec =  StringToNumber(TimSt (Now, '\h'))*3600 +StringToNumber(TimSt (Now, '\i'))*60+StringToNumber(TimSt (Now, '\s'));
IF (SUBST ( TM1User, 1,3) @='DOM' ) ;
   vUSER = SUBST(AttrS('}Clients',TM1User,'}TM1_DefaultDisplayValue'),7,100);
ELSEIF  (SUBST ( TM1User, 1,3) @='Cog' ) ; 
    vUSER = SUBST(AttrS('}Clients',TM1User,'}TM1_DefaultDisplayValue'),16,100);
ELSE ;
    vUSER ='Admin';
ENDIF;
vPRO_NAME = GetProcessName();
vSTATUS='KO';
vNb_lines_recorded=0;
vNb_lines_recorded_Metadata=0;
vNb_lines_recorded_Data=0;
vNb_lines=0;
err_prolog=0;
message_err_prolog='';


#vClient = AttrS('}Clients',TM1User,'}TM1_DefaultDisplayValue');

#--------------------Vues--------------------#
view_erase = 'z_erase_' | vUSER |'_' | vPRO_NAME;
view_copy = 'z_copy_' | vUSER |'_' | vPRO_NAME;

#--------------------Sous-ensembles--------------------#
subset_erase = 'z_erase_' | vUSER |'_' | vPRO_NAME;
subset_copy = 'z_copy_' | vUSER |'_' | vPRO_NAME;

#=========================================#
#   Initialisation du subset à supprimer  #
#=========================================#

#-----------Destruction des objets--------#
ViewDestroy( C_ElementSecurity_Sales_Org, view_erase );
SubsetDestroy(D_Sales_Org, subset_erase);
SubsetDestroy(D_Groups, subset_erase);

#-----------Création des objets-----------#
#Vue
ViewCreate( C_ElementSecurity_Sales_Org, view_erase, 1 );


#D_Sales_Group
MDX = '{{[1_G_SALES_ORG].[TOTAL SALES ORG]},{TM1SUBSETALL([1_G_SALES_ORG])}}';
SubsetCreatebyMDX( subset_erase, MDX, 1 );
SubsetElementDelete( D_Sales_Org, subset_erase, 1 );

#D_Groups
MDX = '{
	{[}Groups].[ADMIN]},
	{[}Groups].[Functional_Admin_CDG]},
	{EXCEPT(
		{TM1SUBSETALL([}Groups])} , 		
		TM1FILTERBYPATTERN(
			{TM1SUBSETALL([}Groups])} , 
			"*ADMIN*"
			)
		)
	}
}';
SubsetCreatebyMDX( subset_erase, MDX, 1 );
SubsetElementDelete( D_Groups, subset_erase, 1 );

#-----Affectation des sous-ensembles à la vue---#
ViewSubsetAssign(C_ElementSecurity_Sales_Org, view_erase, D_Sales_Org, subset_erase);
ViewSubsetAssign(C_ElementSecurity_Sales_Org, view_erase, D_Groups, subset_erase);

#--------------------Suppression des cellules vides, consolidées, calculées--------------------#
ViewSetSkipRuleValues(C_ElementSecurity_Sales_Org, view_erase,1);
ViewSetSkipCalcs(C_ElementSecurity_Sales_Org,view_erase, 0);
ViewSetSkipZeroes(C_ElementSecurity_Sales_Org,view_erase, 0);

#-------définition du subset source-------#
ViewZeroOut( C_ElementSecurity_Sales_Org, view_erase );

#-----------Destruction des objets--------#
ViewDestroy( C_ElementSecurity_Sales_Org, view_erase );
SubsetDestroy(D_Sales_Org, subset_erase);
SubsetDestroy(D_Groups, subset_erase);


#=========================================#
#   Initialisation du subset à copier     #
#=========================================#

#-----------Destruction des objets--------#
ViewDestroy( C_ElementSecurity_Sales_Org, view_copy );
SubsetDestroy(D_Sales_Org, subset_copy);
SubsetDestroy(D_Groups, subset_copy);

#-----------Création des objets-----------#
#Vue
ViewCreate( C_ElementSecurity_Sales_Org, view_copy, 1 );


#D_Sales_Group
MDX = '{{[1_G_SALES_ORG].[TOTAL SALES ORG]},{TM1SUBSETALL([1_G_SALES_ORG])}}';
SubsetCreatebyMDX( subset_copy, MDX, 1 );
SubsetElementDelete( D_Sales_Org, subset_copy, 1 );

#D_Groups
MDX = '{
	{[}Groups].[ADMIN]},
	{[}Groups].[Functional_Admin_CDG]},
	{EXCEPT(
		{TM1SUBSETALL([}Groups])} , 		
		TM1FILTERBYPATTERN(
			{TM1SUBSETALL([}Groups])} , 
			"*ADMIN*"
			)
		)
	}
}';
SubsetCreatebyMDX( subset_copy, MDX, 1 );
SubsetElementDelete( D_Groups, subset_copy, 1 );

#-----Affectation des sous-ensembles à la vue---#
ViewSubsetAssign(C_ElementSecurity_Sales_Org, view_copy, D_Sales_Org, subset_copy);
ViewSubsetAssign(C_ElementSecurity_Sales_Org, view_copy, D_Groups, subset_copy);

#--------------------Suppression des cellules vides, consolidées, calculées--------------------#
ViewSetSkipRuleValues(C_ElementSecurity_Sales_Org, view_copy,1);
ViewSetSkipCalcs(C_ElementSecurity_Sales_Org,view_copy, 0);
ViewSetSkipZeroes(C_ElementSecurity_Sales_Org,view_copy, 0);

#-------définition du subset source-------#
DatasourceCubeview = view_copy;
#endregion
#region Metadata
#****Begin: Generated Statements***
#****End: Generated Statements****

#endregion
#region Data
#****Begin: Generated Statements***
#****End: Generated Statements****



vELEM_SALES_ORG = DimensionElementPrincipalName( D_Sales_Org, vELEM_SALES_ORG );
vELEM_Groups = DimensionElementPrincipalName( D_Groups, vELEM_Groups );

If(SCAN(vELEM_SALES_ORG, vELEM_Groups)<>0 % vELEM_Groups @= 'Agents' % AttrN(D_Groups, vELEM_Groups, 'Sales')=0);
	CellPutS('WRITE', C_ElementSecurity_Sales_Org, vELEM_SALES_ORG, vELEM_Groups);
EndIf;

If(vELEM_Groups @= '16XX' & SCAN('16',vELEM_SALES_ORG )<>0);
	CellPutS('WRITE', C_ElementSecurity_Sales_Org, vELEM_SALES_ORG, vELEM_Groups);
EndIf;

If(vELEM_Groups @= '17XX' & SCAN('17',vELEM_SALES_ORG )<>0);
	CellPutS('WRITE', C_ElementSecurity_Sales_Org, vELEM_SALES_ORG, vELEM_Groups);
EndIf;


If(vELEM_SALES_ORG@= 'TOTAL SALES ORG');
	Old_Value = CellGetS(C_ElementSecurity_Sales_Org, vELEM_SALES_ORG, vELEM_Groups);
	If(Old_Value @= '');
    	CellPutS('READ', C_ElementSecurity_Sales_Org, vELEM_SALES_ORG, vELEM_Groups);
    EndIf;
EndIf;
#endregion
#region Epilog
#****Begin: Generated Statements***
#****End: Generated Statements****


#-----------Destruction des objets--------#
ViewDestroy( C_ElementSecurity_Sales_Org, view_copy );
SubsetDestroy(D_Sales_Org, subset_copy);
SubsetDestroy(D_Groups, subset_copy);



#endregion