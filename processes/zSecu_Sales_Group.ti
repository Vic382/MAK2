#region Prolog
#****Begin: Generated Statements***
#****End: Generated Statements****

################################################################
#                 DESCRIPTION DU PROCESSUS                     #
#                                                              #
# Date :   11/04/2019                                          #
#                                                              #
# Fait par :   Anne BESNARD                                    #
#                                                              #
# Action : Sécurité sur les sales group					       #
#															   #
#                                                              #
################################################################

#====================================#
#     Déclaration des variables      #
#====================================#

#--------------------Cubes--------------------#
C_ElementSecurity_Sales_Group = '}ElementSecurity_SLS_SALES_GROUP';

#--------------------Dimensions--------------------#
D_Sales_Group = 'SLS_SALES_GROUP';
D_Groups = '}Groups';

#--------------------Elements--------------------#



#--------------------Variables globales--------------------#
vDATE = today(1) | ' ' | TIME;
vLocalTime =  '_' | today(1) | '_' | subst(TIME,1,2) | 'h' | subst(TIME,4,2);
vTimeStart_Sec =  StringToNumber(TimSt (Now, '\h'))*3600 +StringToNumber(TimSt (Now, '\i'))*60+StringToNumber(TimSt (Now, '\s'));
IF (SUBST ( TM1User, 1,3) @='DOM' ) ;
   vUSER = SUBST(AttrS('}Clients',TM1User,'}TM1_DefaultDisplayValue'),7,100);
ELSEIF  (SUBST ( TM1User, 1,3) @='Cog' ) ; 
    vUSER = SUBST(AttrS('}Clients',TM1User,'}TM1_DefaultDisplayValue'),16,100);
ELSE ;
    vUSER ='Admin';
ENDIF;
vPRO_NAME = GetProcessName();
vSTATUS='KO';
vNb_lines_recorded=0;
vNb_lines_recorded_Metadata=0;
vNb_lines_recorded_Data=0;
vNb_lines=0;
err_prolog=0;
message_err_prolog='';


#vClient = AttrS('}Clients',TM1User,'}TM1_DefaultDisplayValue');

#--------------------Vues--------------------#
view_erase = 'z_erase_' | vUSER |'_' | vPRO_NAME;
view_copy = 'z_copy_' | vUSER |'_' | vPRO_NAME;

#--------------------Sous-ensembles--------------------#
subset_erase = 'z_erase_' | vUSER |'_' | vPRO_NAME;
subset_copy = 'z_copy_' | vUSER |'_' | vPRO_NAME;

#=========================================#
#   Initialisation du subset à supprimer  #
#=========================================#

#-----------Destruction des objets--------#
ViewDestroy( C_ElementSecurity_Sales_Group, view_erase );
SubsetDestroy(D_Sales_Group, subset_erase);
SubsetDestroy(D_Groups, subset_erase);

#-----------Création des objets-----------#
#Vue
ViewCreate( C_ElementSecurity_Sales_Group, view_erase, 1 );


#D_Sales_Group
MDX = '{{[SLS_SALES_GROUP].[Total Sales Group]},{TM1SUBSETALL([SLS_SALES_GROUP])}}';
SubsetCreatebyMDX( subset_erase, MDX, 1 );
SubsetElementDelete( D_Sales_Group, subset_erase, 1 );

#D_Groups
MDX = '{
	{[}Groups].[ADMIN]},
	{[}Groups].[Functional_Admin_CDG]},
	{EXCEPT(
		{TM1SUBSETALL([}Groups])} , 		
		TM1FILTERBYPATTERN(
			{TM1SUBSETALL([}Groups])} , 
			"*ADMIN*"
			)
		)
	}
}';
SubsetCreatebyMDX( subset_erase, MDX, 1 );
SubsetElementDelete( D_Groups, subset_erase, 1 );

#-----Affectation des sous-ensembles à la vue---#
ViewSubsetAssign(C_ElementSecurity_Sales_Group, view_erase, D_Sales_Group, subset_erase);
ViewSubsetAssign(C_ElementSecurity_Sales_Group, view_erase, D_Groups, subset_erase);

#--------------------Suppression des cellules vides, consolidées, calculées--------------------#
ViewSetSkipRuleValues(C_ElementSecurity_Sales_Group, view_erase,1);
ViewSetSkipCalcs(C_ElementSecurity_Sales_Group,view_erase, 0);
ViewSetSkipZeroes(C_ElementSecurity_Sales_Group,view_erase, 0);

#-------définition du subset source-------#
ViewZeroOut( C_ElementSecurity_Sales_Group, view_erase );

#-----------Destruction des objets--------#
ViewDestroy( C_ElementSecurity_Sales_Group, view_erase );
SubsetDestroy(D_Sales_Group, subset_erase);
SubsetDestroy(D_Groups, subset_erase);


#=========================================#
#   Initialisation du subset à copier     #
#=========================================#

#-----------Destruction des objets--------#
ViewDestroy( C_ElementSecurity_Sales_Group, view_copy );
SubsetDestroy(D_Sales_Group, subset_copy);
SubsetDestroy(D_Groups, subset_copy);

#-----------Création des objets-----------#
#Vue
ViewCreate( C_ElementSecurity_Sales_Group, view_copy, 1 );


#D_Sales_Group
MDX = '{{[SLS_SALES_GROUP].[Total Sales Group]},{TM1SUBSETALL([SLS_SALES_GROUP])}}';
SubsetCreatebyMDX( subset_copy, MDX, 1 );
SubsetElementDelete( D_Sales_Group, subset_copy, 1 );

#D_Groups
MDX = '{
	{[}Groups].[ADMIN]},
	{[}Groups].[Functional_Admin_CDG]},
	{EXCEPT(
		{TM1SUBSETALL([}Groups])} , 		
		TM1FILTERBYPATTERN(
			{TM1SUBSETALL([}Groups])} , 
			"*ADMIN*"
			)
		)
	}
}';
SubsetCreatebyMDX( subset_copy, MDX, 1 );
SubsetElementDelete( D_Groups, subset_copy, 1 );

#-----Affectation des sous-ensembles à la vue---#
ViewSubsetAssign(C_ElementSecurity_Sales_Group, view_copy, D_Sales_Group, subset_copy);
ViewSubsetAssign(C_ElementSecurity_Sales_Group, view_copy, D_Groups, subset_copy);

#--------------------Suppression des cellules vides, consolidées, calculées--------------------#
ViewSetSkipRuleValues(C_ElementSecurity_Sales_Group, view_copy,1);
ViewSetSkipCalcs(C_ElementSecurity_Sales_Group,view_copy, 0);
ViewSetSkipZeroes(C_ElementSecurity_Sales_Group,view_copy, 0);

#-------définition du subset source-------#
DatasourceCubeview = view_copy;
#endregion
#region Metadata
#****Begin: Generated Statements***
#****End: Generated Statements****

#endregion
#region Data
#****Begin: Generated Statements***
#****End: Generated Statements****

vELEM_SALES_GROUP = DimensionElementPrincipalName( D_Sales_Group, vELEM_SALES_GROUP );
vELEM_Groups = DimensionElementPrincipalName( D_Groups, vELEM_Groups );

If(SCAN(vELEM_SALES_GROUP, vELEM_Groups)<>0 % vELEM_Groups @= 'Agents' % AttrN(D_Groups, vELEM_Groups, 'Sales')=0);
	CellPutS('WRITE', C_ElementSecurity_Sales_Group, vELEM_SALES_GROUP, vELEM_Groups);
EndIf;

#On donne accès à tous les groupes retail sur le sales group Admin Retail = N/A Sales Group
If(SCAN('_10',vELEM_GROUPS)<>0);
	CellPutS('WRITE', C_ElementSecurity_Sales_Group, 'N/A Sales Group', vELEM_Groups);
EndIf;

#Sales Manager associé au group (sales group que l'on parcourt)
Sales_Manager_1 = DimensionElementPrincipalName(D_SALES_GROUP, AttrS(D_SALES_GROUP, vELEM_SALES_GROUP, 'Sales Manager 1'));
Sales_Manager_2 = DimensionElementPrincipalName(D_SALES_GROUP, AttrS(D_SALES_GROUP, vELEM_SALES_GROUP, 'Sales Manager 2'));
Sales_Director_1 = DimensionElementPrincipalName(D_SALES_GROUP, AttrS(D_SALES_GROUP, vELEM_SALES_GROUP, 'Sales Director 1'));
Sales_Director_2 = DimensionElementPrincipalName(D_SALES_GROUP, AttrS(D_SALES_GROUP, vELEM_SALES_GROUP, 'Sales Director 2'));
Old_Value = '';
If(Sales_Manager_1 @<> '' & Sales_Manager_1 @<> AttrS(D_SALES_GROUP, vELEM_SALES_GROUP, 'Code - Caption'));
	Old_Value = CellGetS(C_ElementSecurity_Sales_Group, vELEM_SALES_GROUP, Sales_Manager_1|'_Manager' );
	If(Old_Value @= '');
		CellPutS('READ', C_ElementSecurity_Sales_Group, vELEM_SALES_GROUP, Sales_Manager_1|'_Manager' );
	EndIf;
    If(vELEM_SALES_GROUP @= Sales_Manager_1);
    	CellPutS('WRITE', C_ElementSecurity_Sales_Group, vELEM_SALES_GROUP, Sales_Manager_1|'_Manager' );
    EndIf;
EndIf;

Old_Value = '';
If(Sales_Manager_2 @<> '' & Sales_Manager_2 @<> AttrS(D_SALES_GROUP, vELEM_SALES_GROUP, 'Code - Caption'));
	Old_Value = CellGetS(C_ElementSecurity_Sales_Group, vELEM_SALES_GROUP, Sales_Manager_2|'_Manager' );
	If(Old_Value @= '');
		CellPutS('READ', C_ElementSecurity_Sales_Group, vELEM_SALES_GROUP, Sales_Manager_2|'_Manager' );
	EndIf;
    If(vELEM_SALES_GROUP @= Sales_Manager_2);
    	CellPutS('WRITE', C_ElementSecurity_Sales_Group, vELEM_SALES_GROUP, Sales_Manager_2|'_Manager' );
    EndIf;
EndIf;

Old_Value = '';
If(Sales_Director_1 @<> '' & Sales_Director_1 @<> AttrS(D_SALES_GROUP, vELEM_SALES_GROUP, 'Code - Caption'));
	Old_Value = CellGetS( C_ElementSecurity_Sales_Group, vELEM_SALES_GROUP, Sales_Director_1|'_Manager'  );
	If(Old_Value @= '');
		CellPutS('READ', C_ElementSecurity_Sales_Group, vELEM_SALES_GROUP, Sales_Director_1|'_Manager'  );
	EndIf;
    If(vELEM_SALES_GROUP @= Sales_Director_1);
    	CellPutS('WRITE', C_ElementSecurity_Sales_Group, vELEM_SALES_GROUP, Sales_Director_1|'_Manager'  );
    EndIf;
EndIf;

Old_Value = '';
If(Sales_Director_2 @<> '' & Sales_Director_2 @<> AttrS(D_SALES_GROUP, vELEM_SALES_GROUP, 'Code - Caption'));
	Old_Value = CellGetS(  C_ElementSecurity_Sales_Group, vELEM_SALES_GROUP, Sales_Director_2|'_Manager'  );
	If(Old_Value @= '');
		CellPutS('READ', C_ElementSecurity_Sales_Group, vELEM_SALES_GROUP, Sales_Director_2|'_Manager'  );
	EndIf;
    If(vELEM_SALES_GROUP @= Sales_Director_2);
    	CellPutS('WRITE', C_ElementSecurity_Sales_Group, vELEM_SALES_GROUP, Sales_Director_2|'_Manager'  );
    EndIf;
EndIf;
#endregion
#region Epilog
#****Begin: Generated Statements***
#****End: Generated Statements****


#-----------Destruction des objets--------#
ViewDestroy( C_ElementSecurity_Sales_Group, view_copy );
SubsetDestroy(D_Sales_Group, subset_copy);
SubsetDestroy(D_Groups, subset_copy);


#endregion