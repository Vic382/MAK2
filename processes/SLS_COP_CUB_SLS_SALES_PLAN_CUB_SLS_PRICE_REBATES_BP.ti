#region Prolog
#****Begin: Generated Statements***
#****End: Generated Statements****


################################################################
#                                               DESCRIPTION DU PROCESSUS                                              #
#                                                                                                                                                   #
# Date :   31/10/2018                                                                                                                  #
#                                                                                                                                                   #
# Fait par :    Elisabeth MOUNIER                                                                                                #
#                                                                                                                                                   #
# Action : Processus de copie des conditions commerciales  'Rebates et Retail services' 	     #
#	dans le cube des prévisions de vente 					     #
#					                			      #
#     Modifié le 02/04/2019 par Elisabeth Mounier
#	Action : adapter le process aux nouveaux cubes Price et Sales Plan                                                                                                                                               #
#################################################################

##########################
#SPECIFIQUE B2B BOULPAT et Retail RHF
##############################

#====================================#
#             Déclaration des variables                   #
#====================================#

#--------------------Cubes--------------------#
C_Cube_Source = 'SLS_SALES_PLAN';
C_Cube_Cible = 'SLS_SALES_PLAN';
C_Cube_Price = 'SLS_PRICE'; 
C_Cube_Select = 'zSELECT'; 
C_zPROCESS_ADMINISTRATION = 'zPROCESS_ADMINISTRATION';

#--------------------Dimensions--------------------#
D_PHASE = '1_G_CURRENT_PHASE';
D_PERIOD = '1_G_PERIOD';
D_SALES_ORG = '1_G_SALES_ORG';
D_DISTRIB_CHANNEL = '1_G_DISTRIB_CHANNEL';
D_CUSTOMER = 'SLS_CUSTOMER';
D_MATERIAL_PLAN = 'SLS_MATERIAL_PLAN';
D_SALES_GROUP = 'SLS_SALES_GROUP';
D_PRICE_IND='SLS_PRICE_IND';

D_CURRENCY = '1_G_CURRENCY';
D_VOLUME_UNIT = '1_G_VOLUME_UNIT';
D_SALES_PLAN_IND = 'SLS_SALES_PLAN_IND';


#--------------------Elements--------------------#
e_Message_Prolog = '';



#--------------------Variables globales--------------------#
vDATE = today(1) | ' ' | TIME;
vLocalTime =  '_' | today(1) | '_' | subst(TIME,1,2) | 'h' | subst(TIME,4,2);
vTimeStart_Sec =  StringToNumber(TimSt (Now, '\h'))*3600 +StringToNumber(TimSt (Now, '\i'))*60+StringToNumber(TimSt (Now, '\s'));
IF (SUBST ( TM1User, 1,3) @='DOM' ) ;
   vUSER = SUBST(AttrS('}Clients',TM1User,'}TM1_DefaultDisplayValue'),7,100);
ELSEIF  (SUBST ( TM1User, 1,3) @='Cog' ) ; 
    vUSER = SUBST(AttrS('}Clients',TM1User,'}TM1_DefaultDisplayValue'),16,100);
ELSE ;
    vUSER ='Admin';
ENDIF;
vPRO_NAME = GetProcessName();
vSTATUS='KO';
vNb_lines_recorded=0;
vNb_lines_recorded_Metadata=0;
vNb_lines_recorded_Data=0;
vNb_lines=0;
err_prolog=0;
message_err_prolog='';

vClient = AttrS('}Clients',TM1User,'}TM1_DefaultDisplayValue');

#--------------------Vues--------------------#
view_erase = 'z_erase_' | vUSER |'_' | vPRO_NAME;
view_copy = 'z_copy_' | vUSER |'_' | vPRO_NAME;

#--------------------Sous-ensembles--------------------#
subset_erase = 'z_erase_' | vUSER |'_' | vPRO_NAME;
subset_copy = 'z_copy_' | vUSER |'_' | vPRO_NAME;

#-------------------- Elements --------------------#

P_PHASE = CellGetS(C_Cube_Select, vClient, 'Current Phase');
P_SALES_GROUP = CellGetS(C_Cube_Select, vClient, 'Sales Group');

If(P_SALES_GROUP @= '');
	P_SALES_GROUP = 'TOTAL_SALES_GROUP';
EndIf;


DatasourceASCIIDecimalSeparator='.';
DatasourceASCIIThousandSeparator='';

#====================================#
#   Désactivation de la consignation #
#====================================#

OldCubeLogChanges = CUBEGETLOGCHANGES(C_Cube_Cible);
CUBESETLOGCHANGES(C_Cube_Cible, 0);


#============================================#
#                  Initialisation de la vue à supprimer                 #
#============================================#

#--------------------Destruction des objets--------------------#
ViewDestroy(C_Cube_Cible,view_erase);
Subsetdestroy(D_PHASE,subset_erase);
Subsetdestroy(D_SALES_GROUP,subset_erase);
Subsetdestroy(D_SALES_PLAN_IND,subset_erase);
Subsetdestroy(D_CUSTOMER,subset_erase);
Subsetdestroy(D_DISTRIB_CHANNEL,subset_erase);
Subsetdestroy(D_MATERIAL_PLAN,subset_erase);

#--------------------Création des objets--------------------#
ViewCreate(C_Cube_Cible,view_erase);

#D_PHASE
subsetcreate(D_PHASE,subset_erase);
SubsetElementInsert(D_PHASE, subset_erase, P_PHASE, 1);

#D_SALES_GROUP
subsetcreate(D_SALES_GROUP,subset_erase);
SubsetElementInsert(D_SALES_GROUP, subset_erase, P_SALES_GROUP, 1);

#D_DISTRIB_CHANNEL
subsetcreate(D_DISTRIB_CHANNEL,subset_erase);
SubsetElementInsert(D_DISTRIB_CHANNEL, subset_erase, '10', 1);
SubsetElementInsert(D_DISTRIB_CHANNEL, subset_erase, '20', 1);

#D_CUSTOMER
#Modif VRT 16/03/2023 : Modification suite intégration des customer RHF dans les livres GMS
#MDX_CUSTOMER = '{{[SLS_CUSTOMER].[TOTAL_CUSTOMER]},{TM1FILTERBYLEVEL( {DESCENDANTS( [SLS_CUSTOMER].[IC1 - E00070] )}, 0)}, {TM1FILTERBYLEVEL( {DESCENDANTS( [SLS_CUSTOMER].[IC1 - E00095] )}, 0)}}';
MDX_CUSTOMER = '{{[SLS_CUSTOMER].[TOTAL_CUSTOMER]},{TM1FILTERBYLEVEL( {DESCENDANTS( [SLS_CUSTOMER].[IC1 - E00070] )}, 0)}}';
SubsetCreateByMDX(subset_erase, MDX_CUSTOMER);
SubsetElementDelete( D_CUSTOMER, subset_erase, 1);

#D_MATERIAL_PLAN
MDX_MATERIAL_PLAN = '{{[SLS_MATERIAL_PLAN].[TOTAL_PLANNING_MATERIAL]},{TM1FILTERBYLEVEL( {DESCENDANTS([SLS_MATERIAL_PLAN].[Sugar])}, 0)}}';
SubsetCreateByMDX(subset_erase, MDX_MATERIAL_PLAN);
SubsetElementDelete( D_MATERIAL_PLAN, subset_erase, 1);

#D_SALES_PLAN_IND
subsetcreate(D_SALES_PLAN_IND,subset_erase);
SubsetElementInsert(D_SALES_PLAN_IND, subset_erase, 'REB_PER', 1);
SubsetElementInsert(D_SALES_PLAN_IND, subset_erase, 'CUST_REBATE_UNIT', 1);
SubsetElementInsert(D_SALES_PLAN_IND, subset_erase, 'RET_SERV_PER', 1);
SubsetElementInsert(D_SALES_PLAN_IND, subset_erase, 'RET_SERV_UNIT', 1);

#--------------------Affectation des sous-ensembles à la vue--------------------#
ViewSubsetAssign(C_Cube_Cible,view_erase, D_PHASE,subset_erase);
ViewSubsetAssign(C_Cube_Cible,view_erase, D_SALES_GROUP,subset_erase);
ViewSubsetAssign(C_Cube_Cible,view_erase, D_PERIOD,'Prev Periods');
ViewSubsetAssign(C_Cube_Cible,view_erase, D_SALES_PLAN_IND,subset_erase);
ViewSubsetAssign(C_Cube_Cible,view_erase, D_CUSTOMER,subset_erase);
ViewSubsetAssign(C_Cube_Cible,view_erase, D_DISTRIB_CHANNEL,subset_erase);
ViewSubsetAssign(C_Cube_Cible,view_erase, D_MATERIAL_PLAN,subset_erase);

#--------------------Suppression des cellules vides, consolidées, calculées--------------------#
ViewSetSkipRuleValues(C_Cube_Cible,view_erase,1);
ViewSetSkipCalcs(C_Cube_Cible,view_erase, 1);
ViewSetSkipZeroes(C_Cube_Cible,view_erase, 1);

#--------------------Suppression des données--------------------#
ViewZeroOut(C_Cube_Cible,view_erase);

#--------------------Destruction des objets--------------------#
ViewDestroy(C_Cube_Cible,view_erase);
Subsetdestroy(D_PHASE,subset_erase);
Subsetdestroy(D_SALES_GROUP,subset_erase);
Subsetdestroy(D_SALES_PLAN_IND,subset_erase);
Subsetdestroy(D_CUSTOMER,subset_erase);
Subsetdestroy(D_DISTRIB_CHANNEL,subset_erase);
Subsetdestroy(D_MATERIAL_PLAN,subset_erase);



#--------------------Destruction des objets--------------------#
ViewDestroy(C_Cube_Source,view_copy);
SubsetDestroy(D_PHASE, subset_copy);
SubsetDestroy(D_SALES_GROUP, subset_copy);
SubsetDestroy(D_SALES_ORG, subset_copy);
SubsetDestroy(D_CUSTOMER, subset_copy);
SubsetDestroy(D_SALES_PLAN_IND, subset_copy);
SubsetDestroy(D_DISTRIB_CHANNEL, subset_copy);
SubsetDestroy(D_VOLUME_UNIT, subset_copy);
SubsetDestroy(D_CURRENCY, subset_copy);
SubsetDestroy(D_MATERIAL_PLAN, subset_copy);


#--------------------Création des objets--------------------#

ViewCreate(C_Cube_Source,view_copy);

#D_PHASE
subsetcreate(D_PHASE,subset_copy);
SubsetElementInsert(D_PHASE, subset_copy, P_PHASE, 1);

#D_SALES_GROUP
subsetcreate(D_SALES_GROUP,subset_copy);
SubsetElementInsert(D_SALES_GROUP, subset_copy, P_SALES_GROUP, 1);

#D_SALES_ORG
MDX_SALES_ORG = '{{[1_G_SALES_ORG].[TOTAL SALES ORG]},{TM1FILTERBYLEVEL({TM1SUBSETALL([1_G_SALES_ORG])},0)}}';
SubsetCreateByMDX(subset_copy, MDX_SALES_ORG);
SubsetElementDelete( D_SALES_ORG, subset_copy, 1);

#D_MATERIAL_PLAN
MDX_MATERIAL_PLAN = '{{[SLS_MATERIAL_PLAN].[TOTAL_PLANNING_MATERIAL]},{TM1FILTERBYLEVEL( {DESCENDANTS([SLS_MATERIAL_PLAN].[Sugar])}, 0)}}';
SubsetCreateByMDX(subset_copy, MDX_MATERIAL_PLAN);
SubsetElementDelete( D_MATERIAL_PLAN, subset_copy, 1);

#D_CUSTOMER
#Modif VRT 16/03/2023 : Modification suite intégration des customer RHF dans les livres GMS
#MDX_CUSTOMER = '{{[SLS_CUSTOMER].[TOTAL_CUSTOMER]},{TM1FILTERBYLEVEL( {DESCENDANTS( [SLS_CUSTOMER].[IC1 - E00070] )}, 0)}, {TM1FILTERBYLEVEL( {DESCENDANTS( [SLS_CUSTOMER].[IC1 - E00095] )}, 0)}}';
MDX_CUSTOMER = '{{[SLS_CUSTOMER].[TOTAL_CUSTOMER]},{TM1FILTERBYLEVEL( {DESCENDANTS( [SLS_CUSTOMER].[IC1 - E00070] )}, 0)}}';
SubsetCreateByMDX(subset_copy, MDX_CUSTOMER);
SubsetElementDelete(D_CUSTOMER, subset_copy, 1);

#D_SALES_PLAN_IND
#VRT : Mise en place des contrats indexe
#MDX_SALES_PLAN_IND = '{{[SLS_SALES_PLAN_IND].[SLS_VOL]},{FILTER( {TM1SUBSETALL( [SLS_SALES_PLAN_IND] )}, [SLS_SALES_PLAN_IND].[Type_Indicateur] = "Volume")}}';
MDX_SALES_PLAN_IND = '{ EXCEPT({FILTER( {TM1SUBSETALL( [SLS_SALES_PLAN_IND] )}, [SLS_SALES_PLAN_IND].[Type_Indicateur] = "Volume")},{TM1FILTERBYPATTERN( {TM1SUBSETALL( [SLS_SALES_PLAN_IND] )}, "I_*")})}';
SubsetCreateByMDX(subset_copy, MDX_SALES_PLAN_IND);
SubsetElementDelete(D_SALES_PLAN_IND, subset_copy, 1);
nCheckIndex = SubsetElementGetIndex ( D_SALES_PLAN_IND, subset_copy , 'SLS_VOL_INDEXE',1) ;
SubsetElementDelete(D_SALES_PLAN_IND, subset_copy, nCheckIndex );

#D_DISTRIB_CHANNEL
subsetcreate(D_DISTRIB_CHANNEL,subset_copy);
SubsetElementInsert(D_DISTRIB_CHANNEL, subset_copy, '10', 1);
SubsetElementInsert(D_DISTRIB_CHANNEL, subset_copy, '20', 1);

#D_CURRENCY
subsetcreate(D_CURRENCY,subset_copy);
SubsetElementInsert(D_CURRENCY, subset_copy, 'EUR', 1);

#D_VOLUME_UNIT
subsetcreate(D_VOLUME_UNIT,subset_copy);
SubsetElementInsert(D_VOLUME_UNIT, subset_copy, 'TO', 1);


#--------------------Affectation des sous-ensembles à la vue--------------------#
ViewSubsetAssign(C_Cube_Source,view_copy, D_PHASE,subset_copy);
ViewSubsetAssign(C_Cube_Source,view_copy, D_SALES_GROUP,subset_copy);
ViewSubsetAssign(C_Cube_Source,view_copy, D_SALES_ORG,subset_copy);
ViewSubsetAssign(C_Cube_Source,view_copy, D_CUSTOMER,subset_copy);
ViewSubsetAssign(C_Cube_Source,view_copy, D_SALES_PLAN_IND,subset_copy);
ViewSubsetAssign(C_Cube_Source,view_copy, D_DISTRIB_CHANNEL,subset_copy);
ViewSubsetAssign(C_Cube_Source,view_copy, D_CURRENCY,subset_copy);
ViewSubsetAssign(C_Cube_Source,view_copy, D_VOLUME_UNIT,subset_copy);
ViewSubsetAssign(C_Cube_Source,view_copy, D_PERIOD,'Prev Periods');
ViewSubsetAssign(C_Cube_Source,view_copy, D_MATERIAL_PLAN,subset_copy);

#--------------------Suppression des cellules vides, consolidées, calculées--------------------#
ViewSetSkipRuleValues(C_Cube_Source,view_copy,1);
ViewSetSkipCalcs(C_Cube_Source,view_copy, 1);
ViewSetSkipZeroes(C_Cube_Source,view_copy, 1);

# définition de la vue source#

DatasourceCubeview= view_copy;


#endregion
#region Metadata
#****Begin: Generated Statements***
#****End: Generated Statements****
#endregion
#region Data
#Pas de restriction au Planning Material, saisie au N/A_MATERIAL_PLAN
#SI PM/ShipTo vide => voir PM/IC3, sinon etc...
#à copier sur l'indicateur REB_PER


############################################################################
###				   	%REBATES					####
#############################################################################

#récupérer l'indicateur du cube SLS_PRICE (attention notre vue source est celle de sls_plan)

#CAS 1 : check si des prix sont renseignés à la maille PM/ShipTo


Val_Price_REB_PER = CellGetN(C_Cube_Price, vPhase, vPeriod, vSales_Org, vDistrib_Channel, vCustomer, 'N/A_PLANNING_MATERIAL', vSales_Group, 'REB_PER');


#Si shipTo vide
If(Val_Price_REB_PER <> 0);

	Val_REB_PER = Val_Price_REB_PER; 


#Sinon, on check à la maille IC3
#plus précisément sur le N/A_IC3
Else;

	vIC3 = ELPAR(D_CUSTOMER, vCustomer, 1); 
	Val_IC3_REB_PER = CellGetN(C_Cube_Price, vPhase, vPeriod, vSales_Org, vDistrib_Channel, vIC3, 'N/A_PLANNING_MATERIAL', vSales_Group, 'REB_PER');
	

	If(Val_IC3_REB_PER <> 0);
		Val_REB_PER = Val_IC3_REB_PER;

	#Si PM/IC3 non renseigné, on check à la maille NA_IC2
	Else;

		vIC2 = ELPAR(D_CUSTOMER, vIC3, 1);
		Val_IC2_REB_PER = CellGetN(C_Cube_Price, vPhase, vPeriod, vSales_Org, vDistrib_Channel, vIC2, 'N/A_PLANNING_MATERIAL', vSales_Group, 'REB_PER');
		
		
		If(Val_IC2_REB_PER <> 0);

			Val_REB_PER = Val_IC2_REB_PER;

		#Si IC2 non renseigné, on check à la maille NA_IC1
		Else;

			vIC1 = ELPAR(D_CUSTOMER, vIC2, 1);
			Val_IC1_REB_PER = CellGetN(C_Cube_Price, vPhase, vPeriod, vSales_Org, vDistrib_Channel, vIC1, 'N/A_PLANNING_MATERIAL', vSales_Group, 'REB_PER');
			Val_REB_PER = Val_IC1_REB_PER;

		EndIf;
	EndIf;


EndIf;

#on a seulement besoin de copier sur la période lue car pas besoin du prix s'il n'y a pas de volume
CellPutN(Val_REB_PER, C_Cube_Cible, vPhase, vPeriod, vSales_Org, vDistrib_Channel, 'EUR', 'TO', vCustomer, vMaterial_Plan, vSales_Group, 'REB_PER');


############################################################################
###				   	%RETAIL SERVICES				####
#############################################################################

#récupérer l'indicateur du cube SLS_PRICE (attention notre vue source est celle de sls_plan)

#CAS 1 : check si des prix sont renseignés à la maille PM/ShipTo


Val_Price_RET_SERV_PER = CellGetN(C_Cube_Price, vPhase, vPeriod, vSales_Org, vDistrib_Channel, vCustomer, 'N/A_PLANNING_MATERIAL', vSales_Group, 'RET_SERV_PER');
# Val_Price_RET_SERV_UNIT = CellGetN(C_Cube_Price, vPhase, vPeriod, vSales_Org, vDistrib_Channel, vCustomer, 'N/A_PLANNING_MATERIAL', vSales_Group, 'RET_SERV_UNIT');

#Si shipTo vide
If(Val_Price_RET_SERV_PER <> 0 );

		Val_RET_SERV_PER = Val_Price_RET_SERV_PER; 
	

#Sinon, on check à la maille IC3
#plus précisément sur le N/A_IC3
Else;

	vIC3 = ELPAR(D_CUSTOMER, vCustomer, 1);
	Val_IC3_RET_SERV_PER = CellGetN(C_Cube_Price, vPhase, vPeriod, vSales_Org, vDistrib_Channel, vIC3, 'N/A_PLANNING_MATERIAL', vSales_Group, 'RET_SERV_PER');
	

	If(Val_IC3_RET_SERV_PER <> 0);
		Val_RET_SERV_PER = Val_IC3_RET_SERV_PER;

	#Si PM/IC3 non renseigné, on check à la maille NA_IC2
	Else;

		vIC2 = ELPAR(D_CUSTOMER, vIC3, 1);
		Val_IC2_RET_SERV_PER = CellGetN(C_Cube_Price, vPhase, vPeriod, vSales_Org, vDistrib_Channel, vIC2, 'N/A_PLANNING_MATERIAL', vSales_Group, 'RET_SERV_PER');
		
		
		If(Val_IC2_RET_SERV_PER <> 0);

			Val_RET_SERV_PER = Val_IC2_RET_SERV_PER;

		#Si IC2 non renseigné, on check à la maille NA_IC1
		Else;

			vIC1 = ELPAR(D_CUSTOMER, vIC2, 1);
			Val_IC1_RET_SERV_PER = CellGetN(C_Cube_Price, vPhase, vPeriod, vSales_Org, vDistrib_Channel, vIC1, 'N/A_PLANNING_MATERIAL', vSales_Group, 'RET_SERV_PER');
			Val_RET_SERV_PER = Val_IC1_RET_SERV_PER;

		EndIf;
	EndIf;


EndIf;

#on a seulement besoin de copier sur la période lue car pas besoin du prix s'il n'y a pas de volume
CellPutN(Val_RET_SERV_PER, C_Cube_Cible, vPhase, vPeriod, vSales_Org, vDistrib_Channel, 'EUR', 'TO', vCustomer, vMaterial_Plan, vSales_Group, 'RET_SERV_PER');


############################################################################
###				   	UNIT RETAIL SERVICES				####
#############################################################################
#seulement s'il n'y avait rien sur l'indicateur en Pourcentage
#récupérer l'indicateur du cube SLS_PRICE (attention notre vue source est celle de sls_plan)

#CAS 1 : check si des prix sont renseignés à la maille PM/ShipTo

If(Val_RET_SERV_PER = 0);

	Val_Price_RET_SERV_UNIT = CellGetN(C_Cube_Price, vPhase, vPeriod, vSales_Org, vDistrib_Channel, vCustomer, 'N/A_PLANNING_MATERIAL', vSales_Group, 'RET_SERV_UNIT');


	#Si shipTo vide
	If(Val_Price_RET_SERV_UNIT <> 0);

		Val_RET_SERV_UNIT = Val_Price_RET_SERV_UNIT; 


	#Sinon, on check à la maille IC3
	#plus précisément sur le N/A_IC3
	Else;

		vIC3 = ELPAR(D_CUSTOMER, vCustomer, 1);
		Val_IC3_RET_SERV_UNIT = CellGetN(C_Cube_Price, vPhase, vPeriod, vSales_Org, vDistrib_Channel, vIC3, 'N/A_PLANNING_MATERIAL', vSales_Group, 'RET_SERV_UNIT');
	

		If(Val_IC3_RET_SERV_UNIT <> 0);
			Val_RET_SERV_UNIT = Val_IC3_RET_SERV_UNIT;

		#Si PM/IC3 non renseigné, on check à la maille NA_IC2
		Else;

			vIC2 = ELPAR(D_CUSTOMER, vIC3, 1);
			vIC2_Input = 'N/A_' | vIC2; 
			Val_IC2_RET_SERV_UNIT = CellGetN(C_Cube_Price, vPhase, vPeriod, vSales_Org, vDistrib_Channel, vIC2, 'N/A_PLANNING_MATERIAL', vSales_Group, 'RET_SERV_UNIT');
		
		
			If(Val_IC2_RET_SERV_UNIT <> 0);

				Val_RET_SERV_UNIT = Val_IC2_RET_SERV_UNIT;

			#Si IC2 non renseigné, on check à la maille NA_IC1
			Else;

				vIC1 = ELPAR(D_CUSTOMER, vIC2, 1);
				vIC1_Input = 'N/A_' | vIC1; 
				Val_IC1_RET_SERV_UNIT = CellGetN(C_Cube_Price, vPhase, vPeriod, vSales_Org, vDistrib_Channel, vIC1, 'N/A_PLANNING_MATERIAL', vSales_Group, 'RET_SERV_UNIT');
				Val_RET_SERV_UNIT = Val_IC1_RET_SERV_UNIT;

			EndIf;
		EndIf;


	EndIf;

	#on a seulement besoin de copier sur la période lue car pas besoin du prix s'il n'y a pas de volume
	CellPutN(Val_RET_SERV_UNIT, C_Cube_Cible, vPhase, vPeriod, vSales_Org, vDistrib_Channel, 'EUR', 'TO', vCustomer, vMaterial_Plan, vSales_Group, 'RET_SERV_UNIT');

EndIf;

############################################################################
###				   	UNIT REBATES					####
#############################################################################
#seulement s'il n'y avait rien sur l'indicateur en Pourcentage
#récupérer l'indicateur du cube SLS_PRICE (attention notre vue source est celle de sls_plan)

#CAS 1 : check si des prix sont renseignés à la maille PM/ShipTo

If( Val_REB_PER = 0 );

	Val_Price_REB_UNIT = CellGetN(C_Cube_Price, vPhase, vPeriod, vSales_Org, vDistrib_Channel, vCustomer, 'N/A_PLANNING_MATERIAL', vSales_Group, 'REB_UNIT');


	#Si shipTo vide
	If(Val_Price_REB_UNIT <> 0);

		Val_REB_UNIT = Val_Price_REB_UNIT; 


	#Sinon, on check à la maille IC3
	#plus précisément sur le N/A_IC3
	Else;

		vIC3 = ELPAR(D_CUSTOMER, vCustomer, 1);
		Val_IC3_REB_UNIT = CellGetN(C_Cube_Price, vPhase, vPeriod, vSales_Org, vDistrib_Channel, vIC3, 'N/A_PLANNING_MATERIAL', vSales_Group, 'REB_UNIT');
	

		If(Val_IC3_REB_UNIT <> 0);
			Val_REB_UNIT = Val_IC3_REB_UNIT;

		#Si PM/IC3 non renseigné, on check à la maille NA_IC2
		Else;

			vIC2 = ELPAR(D_CUSTOMER, vIC3, 1);
			Val_IC2_REB_UNIT = CellGetN(C_Cube_Price, vPhase, vPeriod, vSales_Org, vDistrib_Channel, vIC2, 'N/A_PLANNING_MATERIAL', vSales_Group, 'REB_UNIT');
		
		
			If(Val_IC2_REB_UNIT <> 0);

				Val_REB_UNIT = Val_IC2_REB_UNIT;

			#Si IC2 non renseigné, on check à la maille NA_IC1
			Else;

				vIC1 = ELPAR(D_CUSTOMER, vIC2, 1);
				Val_IC1_REB_UNIT = CellGetN(C_Cube_Price, vPhase, vPeriod, vSales_Org, vDistrib_Channel, vIC1,'N/A_PLANNING_MATERIAL', vSales_Group, 'REB_UNIT');
				Val_REB_UNIT = Val_IC1_REB_UNIT;

			EndIf;
		EndIf;


	EndIf;

	#on a seulement besoin de copier sur la période lue car pas besoin du prix s'il n'y a pas de volume
	CellPutN(Val_REB_UNIT, C_Cube_Cible, vPhase, vPeriod, vSales_Org, vDistrib_Channel, 'EUR', 'TO', vCustomer, vMaterial_Plan, vSales_Group, 'CUST_REBATE_UNIT');

EndIf;
#endregion
#region Epilog
#****Begin: Generated Statements***
#****End: Generated Statements****


#--------------------Destruction des objets--------------------#
ViewDestroy(C_Cube_Source,view_copy);
SubsetDestroy(D_PHASE, subset_copy);
SubsetDestroy(D_SALES_GROUP, subset_copy);
SubsetDestroy(D_DISTRIB_CHANNEL, subset_copy);
SubsetDestroy(D_SALES_ORG, subset_copy);
SubsetDestroy(D_CUSTOMER, subset_copy);
SubsetDestroy(D_SALES_PLAN_IND, subset_copy);
SubsetDestroy(D_VOLUME_UNIT, subset_copy);
SubsetDestroy(D_CURRENCY, subset_copy);
SubsetDestroy(D_MATERIAL_PLAN, subset_copy);


#====================================#
#      Activation de la consignation  #
#====================================#

OldCubeLogChanges = CUBEGETLOGCHANGES(C_Cube_Cible);
CUBESETLOGCHANGES(C_Cube_Cible, 1);
#endregion